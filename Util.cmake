include_guard(GLOBAL)

set(TAGS_BEGIN_MARKER "======================== TAGS BEGIN ========================")

function(add_to_tags_file MODULE_NAME MODULE_TAG)
  set(TAGS_FILE_PATH "${CMAKE_BINARY_DIR}/TagsFile")

  if (NOT EXISTS ${TAGS_FILE_PATH})
    file(APPEND ${TAGS_FILE_PATH} "${TAGS_BEGIN_MARKER}\n")
  endif()

  file(APPEND ${TAGS_FILE_PATH} "${MODULE_NAME}: ${MODULE_TAG}\n")
endfunction()

function(load_tags_file)
  if (NOT DEFINED CMAKEPKG_TAG_FILE)
    return()
  endif()

  if (NOT EXISTS ${CMAKEPKG_TAG_FILE})
    message(WARNING "Tags file '${CMAKEPKG_TAG_FILE}' does not exist!")
    return()
  endif()

  set(TAGS_BEGIN OFF)

  message(STATUS "Loading Tags File '${CMAKEPKG_TAG_FILE}'")
  file(STRINGS ${CMAKEPKG_TAG_FILE} CMAKEPKG_TAGS REGEX "^[ ]*[^#].*")
  foreach (LINE IN LISTS CMAKEPKG_TAGS)
    if (NOT TAGS_BEGIN)
      if ("${LINE}" STREQUAL "${TAGS_BEGIN_MARKER}")
        set(TAGS_BEGIN ON)
      endif()
      continue()
    endif()

    string(REPLACE " " "" EXPR "${LINE}")
    if (EXPR MATCHES ".*:.*")
      string(REPLACE ":" ";" EXPR "${EXPR}")
      list(GET EXPR 0 PACKAGE_ID)
      list(GET EXPR 1 TAG)
      if (NOT "${PACKAGE_ID}" STREQUAL "")
        string(REPLACE "/" "_" PACKAGE_ID "${PACKAGE_ID}")
        string(TOLOWER "${PACKAGE_ID}" PACKAGE_ID)
        set("${PACKAGE_ID}_TAG" "${TAG}" CACHE INTERNAL "Revision of the ${PACKAGE_ID} package")
      endif()
    else()
      message(WARNING "Ignoring expression in line '${LINE}' of CMAKEPKG_TAG_FILE '${CMAKEPKG_TAG_FILE}'")
    endif()
  endforeach()
endfunction()
